---
${yamlencode(
  {
    for group in groups :
    group.name => {
      vars = {
        ansible_user = ssh_username
        ansible_ssh_private_key_file = ssh_key_file
        ansible_ssh_common_args = can(group.jumphost) ? join(" ", [
          "-o StrictHostKeyChecking=no",
          "-o ControlMaster=auto -o ControlPersist=600s",
          "-o 'ProxyCommand ssh -o StrictHostKeyChecking=no",
            "-i \"${ssh_key_file}\"",
            "-l \"${ssh_username}\" -W %h:%p",
            "${group.jumphost.network_interface.0.nat_ip_address}'"
          ]) : join(" ", [
            "-o StrictHostKeyChecking=no",
            "-o ControlMaster=auto -o ControlPersist=600s",
          ])
      }
      hosts = {
        for host in group.hosts :
        host.hostname => {
          ansible_host = (can(group.jumphost) ?
            host.network_interface.0.ip_address :
            host.network_interface.0.nat_ip_address)
        }
      }
    }
  }
)}
...
